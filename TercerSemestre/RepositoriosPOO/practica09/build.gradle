plugins {
    id 'java'
    id 'war'
}

group 'poo2.progs'
version '34152734'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.7.1'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    compileOnly('javax:javaee-web-api:8.0.1')
    implementation('org.glassfish.jersey.containers:jersey-container-servlet:2.31')
    implementation('org.glassfish.jersey.media:jersey-media-json-jackson:2.31')
    implementation('org.glassfish.jersey.inject:jersey-hk2:2.31')
    implementation('org.glassfish.jersey.core:jersey-client:2.31')
    implementation('org.eclipse.persistence:eclipselink:2.7.7')
    implementation('mysql:mysql-connector-java:8.0.25')

    implementation('javax.xml.bind:jaxb-api:2.3.1')
    implementation('javax.activation:activation:1.1')
    implementation('org.glassfish.jaxb:jaxb-runtime:2.3.1')

    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.+'

    implementation fileTree('lib')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    testImplementation group: "org.dbunit", name: "dbunit", version: "2.7.0"
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    testLogging.showStandardStreams = true
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nResultados de Prueba: ${result.resultType}"
            println "Resumen de prueba: ${result.testCount} pruebas, " +
                    "${result.successfulTestCount} exitosas, " +
                    "${result.failedTestCount} fallidas, " +
                    "${result.skippedTestCount} omitidas"
        }
    }

    systemProperty 'tipo_prueba', System.getProperty('tipo_prueba')
}